import
  std / [os, osproc, strformat, strutils, strtabs],
  json_serialization,
  json_serialization / std / tables,
  ../../common/[ config, ct_logging ]

type
  PathsConfig = Table[string, string]

proc setupEnv*(configPath: string): StringTableRef =
  if not fileExists(configPath):
    echo fmt"error: expected a paths config generated by shell.nix in {configPath}:"
    echo "  please try to go in the codetracer source folder and run `direnv allow` there"
    quit(1)
  var config = Json.decode(readFile(configPath), PathsConfig)
  var env = newStringTable(modeStyleInsensitive)
  for name, value in envPairs():
    env[name] = value

  for name, ctValue in config:
    if name in env:
      env[name] = fmt"{ctValue}:{env[name]}"
    else:
      env[name] = ctValue
  result = env


proc build*(programPath: string, outputPath: string, nimcachePath: string = ""): string =
  let ctConfig = loadConfig(folder=getCurrentDir(), inTest=false)
  if ctConfig.rrBackend.enabled:
    try:
      var args = @["build", programPath]
      if outputPath.len > 0:
        args.add(outputPath)
      if nimcachePath.len > 0:
        args.add("--nimcache=" & nimcachePath)
      # assume for now that the build process prints the `binary` result
      #   if it is succesful
      let p = startProcess(
        ctConfig.rrBackend.path,
        args = args,
        # env = env,
        options = {poStdErrToStdOut})
      let (lines, exitCode) = p.readLines
      echo lines.join("\n") & "\n"
      if exitCode == 0:
        if lines.len > 0 and lines[^1].startsWith("binary:"):
          return lines[^1].split(": ", 1)[1]
        else:
          echo "[codetracer] error: build succesful, but couldn't extract the resulting binary path"
          quit(1)
      else:
        echo "[codetracer] error: build exitted with code ", exitCode
        quit(exitCode)
    except:
      errorPrint "[codetracer] error: ct build plugin error: ", getCurrentExceptionMsg()
      quit(1)
  else:
    for line in rrBackendMissingGuidanceLines():
      errorPrint line
